#!/usr/bin/env -S cmake -P

## Copyright 2018-present Network Optix, Inc. Licensed under MPL 2.0: www.mozilla.org/MPL/2.0/

cmake_minimum_required(VERSION 3.20)

include(CMakeParseArguments)

set(ENV{CONAN_USER_HOME} "@CONAN_USER_HOME@")
set(CONAN_EXECUTABLE "@CONAN_EXECUTABLE@")
set(CMAKE_SOURCE_DIR "@CMAKE_SOURCE_DIR@")
set(CMAKE_BINARY_DIR "@CMAKE_BINARY_DIR@")

set(installSystemRequirements "OFF" CACHE INTERNAL "")

set(conanNxRemote "@conanNxRemote@")
set(conanLockExportDir "@distribution_output_dir@")
set(conanFlags @flags@)

if(installSystemRequirements)
    set(conanFlags
        ${conanFlags}
         "-c tools.system.package_manager:mode=install"
         "-c tools.system.package_manager:sudo=True")
    set(ENV{CONAN_SYSREQUIRES_SUDO} "True")
    set(ENV{CONAN_SYSREQUIRES_MODE} "enabled")
else()
    set(conanFlags
        ${conanFlags}
         "-c tools.system.package_manager:mode=check"
         "-c tools.system.package_manager:sudo=False")
    set(ENV{CONAN_SYSREQUIRES_SUDO} "False")
    set(ENV{CONAN_SYSREQUIRES_MODE} "verify")
endif()

function(run_conan)
    execute_process(
        COMMAND
            ${CONAN_EXECUTABLE} install ${CMAKE_SOURCE_DIR}
            ${conanFlags}
            --install-folder ${CMAKE_BINARY_DIR}
            ${ARGV}
        COMMAND_ECHO STDERR
        RESULT_VARIABLE result
    )

    if(result EQUAL 0)
        set(CONAN_FAILED FALSE PARENT_SCOPE)
    else()
        set(CONAN_FAILED TRUE PARENT_SCOPE)
    endif()
endfunction()

run_conan()

# TODO: Remove the second attempt with `--update` flag when the project is switched to Conan 2.
# Conan 2 can hold multiple revisions in the local cache, thus it does not need this workaround to
# replace downloaded recipe revisions.
if(CONAN_FAILED)
    message(WARNING "Conan failed to install dependencies. Retrying with `--update`.")
    run_conan(--update)
endif()

if(CONAN_FAILED)
    message(FATAL_ERROR "Conan failed to install dependencies.")
endif()

file(COPY "${CMAKE_BINARY_DIR}/conan.lock" DESTINATION "${conanLockExportDir}/")
